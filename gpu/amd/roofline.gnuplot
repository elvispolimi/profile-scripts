if (!exists("outfile")) outfile='roofline-fp.ps'
set term postscript solid color rounded
set output outfile

unset clip points
set clip one
unset clip two
set bar 1.000000
set xdata
set ydata
set zdata
set x2data
set y2data
set boxwidth
set style fill  empty border
set dummy x,y
set format y "10^{%T}"
set format x "10^{%T}"
set format x2 "% g"
set format y2 "% g"
set format z "% g"
set format cb "% g"
set angles radians
set grid nopolar
set grid xtics mxtics ytics mytics noztics nomztics \
 nox2tics nomx2tics noy2tics nomy2tics nocbtics nomcbtics
set grid layerdefault   linetype 0 linewidth 1.000,  linetype 0 linewidth 1.000
set key title ""
# set key off
set key right bottom maxrows 5 font ",10"
unset label
unset arrow
unset style line
unset style arrow
unset logscale
set logscale x 10
set logscale y 10
set offsets 0, 0, 0, 0
set pointsize 1
set encoding default
unset polar
unset parametric
unset decimalsign
set view 60, 30, 1, 1  
set samples 1000, 1000
set isosamples 10, 10
set surface
#unset contour
#set clabel '%8.3g'
set mapping cartesian
set datafile separator whitespace
unset hidden3d
#set cntrparam order 4
#set cntrparam linear
#set cntrparam levels auto 5
#set cntrparam points 5
set size 1,1
set origin 0,0
set style data lines
set style function lines
set xzeroaxis linetype -2 linewidth 1.000
set yzeroaxis linetype -2 linewidth 1.000
set x2zeroaxis linetype -2 linewidth 1.000
set y2zeroaxis linetype -2 linewidth 1.000
set ticslevel 0.5
set mxtics 10
set mytics 10
set mztics default
set mx2tics default
set my2tics default
set mcbtics default
set xtics autofreq
set ytics autofreq
set ztics autofreq
set nox2tics
set noy2tics
set cbtics autofreq
set timestamp bottom 
set timestamp "" 
set rrange [ * : * ] noreverse nowriteback
set trange [ * : * ] noreverse nowriteback
set urange [ * : * ] noreverse nowriteback
set vrange [ * : * ] noreverse nowriteback
set xlabel "Arithmetic Intensity (FLOP/B)"
set x2label "" 
set xrange [1.000000e-02 : 2.00000e+02] noreverse nowriteback
set x2range [ * : * ] noreverse nowriteback
set ylabel "Performance (GFLOP/s)"
set y2label "" 
set yrange [1.000000e+01 : *] noreverse nowriteback
set y2range [ * : * ] noreverse nowriteback
set zlabel "" 
set zrange [ * : * ] noreverse nowriteback
set cblabel "" 
set cbrange [ * : * ] noreverse nowriteback
set zero 1e-08
set lmargin  -1
set bmargin  -1
set rmargin  -1
set tmargin  -1
set locale "C"
set pm3d explicit at s
set pm3d scansautomatic
set palette positive nops_allcF maxcolors 0 gamma 1.5 color model RGB 
set palette rgbformulae 7, 5, 15
set colorbox default
set loadpath 
set fit noerrorvariables

# Device parameters (can be overridden via gnuplot -e)
if (!exists("precision")) precision = "fp"
if (!exists("peak_fp")) peak_fp = 80 * 32 * 2 * 1.53 # GFLOP/s
if (!exists("peak_dp")) peak_dp = peak_fp / 32 # GFLOP/s
if (!exists("peak_nofma_fp")) peak_nofma_fp = peak_fp / 2 # GFLOP/s
if (!exists("peak_nofma_dp")) peak_nofma_dp = peak_dp / 2 # GFLOP/s
if (!exists("peak_int")) {
    if (exists("peak_nofma_fp")) {
        peak_int = peak_nofma_fp
    } else {
        peak_int = peak_fp / 2
    }
}
if (!exists("l1_peak")) l1_peak = 437.5 * 32 # GB/s
if (!exists("l2_peak")) l2_peak = 93.6 * 32 # GB/s
if (!exists("hbm_peak")) hbm_peak = 25.9 * 32 # GB/s

if (precision eq "dp") {
    peak = peak_dp
    peak_nofma = peak_nofma_dp
    precision_label = "FP64"
    metric_unit = "GFLOP/s"
} else if (precision eq "int") {
    peak = peak_int
    peak_nofma = peak_int
    precision_label = "INT32"
    metric_unit = "GINTOP/s"
} else {
    peak = peak_fp
    peak_nofma = peak_nofma_fp
    precision_label = "FP32"
    metric_unit = "GFLOP/s"
}

if (precision eq "int") {
    set xlabel "Integer Arithmetic Intensity (INTOP/B)"
    set ylabel "Performance (GINTOP/s)"
} else {
    set xlabel "Arithmetic Intensity (FLOP/B)"
    set ylabel "Performance (GFLOP/s)"
}

# Points (generated by ncu2dat)
if (!exists("roofline_sp_points")) roofline_sp_points = ""
if (!exists("roofline_dp_points")) roofline_dp_points = ""
if (!exists("roofline_int_points")) roofline_int_points = ""
if (precision eq "dp") {
    roofline_points = roofline_dp_points
} else if (precision eq "int") {
    roofline_points = roofline_int_points
} else {
    roofline_points = roofline_sp_points
}

# Ceilings
l1_ceiling(x) = peak > (x * l1_peak) ? (x * l1_peak) : peak
l2_ceiling(x) = peak > (x * l2_peak) ? (x * l2_peak) : peak
hbm_ceiling(x) = peak > (x * hbm_peak) ? (x * hbm_peak) : peak
peak_ceiling(x) = peak <= (x * l1_peak) ? peak : 1/0
peak_nofma_ceiling(x) = (precision eq "int" ? 1/0 : (peak_nofma <= (x * l1_peak) ? peak_nofma : 1/0))

# Styling
line_width = 2
point_size = 1.5
slope_angle = 45

hbm_color = '#74460b'
l1_color = '#eaa800'
l2_color = '#14967c'

point = 5

# Ceiling labels
label_x = 0.05
set label sprintf('Theoretical peak \@ %s: %.1f %s', precision_label, peak, metric_unit) at 2,peak+1000 textcolor rgb 'black' font ",12"
if (precision ne "int") set label 'FMA' at 180,peak_nofma+500 right textcolor rgb 'black' font ",12"
set label sprintf('L1 %.1f GB/s', l1_peak) at peak_nofma/l1_peak, peak_nofma left rotate by slope_angle textcolor rgb l1_color font ",12"
set label sprintf('L2 %.1f GB/s', l2_peak) at peak_nofma/l2_peak, peak_nofma left rotate by slope_angle textcolor rgb l2_color font ",12"
set label sprintf('HBM %.1f GB/s', hbm_peak)  at peak_nofma/hbm_peak, peak_nofma left rotate by slope_angle textcolor rgb hbm_color font ",12"

plot \
	l1_ceiling(x) lw line_width lc rgb l1_color notitle,\
	l2_ceiling(x) lw line_width lc rgb l2_color notitle,\
	hbm_ceiling(x) lw line_width lc rgb hbm_color notitle,\
	peak_ceiling(x) lw line_width lc rgb 'black' notitle,\
	peak_nofma_ceiling(x) lw line_width lc rgb 'black' dashtype 2 notitle,\
	@roofline_points
